; 
;
;

.define VSYNC 0
.define HSYNC 1

.program vga_pwm_pio 

.out 4 left auto 32
.fifo tx


  set pins, 0

.wrap_target

  out x, 32 ; Load the number of lines in the vertical back porch
  out y, 32 ; Load the number of visible lines.

  ; Wait for a V Sync to start a new frame
  wait 0 pin VSYNC [5]
  wait 1 pin VSYNC [5]

vsync_porch:

  ; Actually wait for the sync pulse
  wait 0 pin HSYNC [5]
  wait 1 pin HSYNC [5]

  jmp x-- vsync_porch

  ; Indicate that this is the first line and we should *not*
  ; dump the vertial info since we've already consumed it.
  set x, 1

next_line:

  ; Dump the vertical info if we haven't consumed it.
  jmp x-- no_dump
  out null, 32
  out null, 32 
no_dump:

  ; Setup hsync back porch counter
  out x, 32 

  ; Actually wait for the sync pulse
  wait 0 pin HSYNC [5]
  wait 1 pin HSYNC [5]

hsync_backporch:
  set pins, 0 [4]
  jmp x-- hsync_backporch

  ; Setup visible pixels counter
  out x, 32

write_pixel:
  ; Write out a pixel
  out pins, 4  [4]
  set pins, 0
  jmp x-- write_pixel

  ; Zero out the video pins, and wait for the next line
  set pins, 0 

  ; jump to the next line 
  jmp y-- next_line

.wrap

% c-sdk {

#include "hardware/clocks.h"

static inline void vga_pwm_pio_program_init(
  PIO pio, uint sm, uint offset, uint32_t raw_pixel_clk, uint out_base_pin, uint sync_in_pin) {

  uint32_t sys_clk = clock_get_hz(clk_sys);

  // Calculate the 8bit part of the 16.8 divider
  uint32_t pixel_clk = raw_pixel_clk * 5;
  uint32_t vga_div16 = sys_clk / pixel_clk;
  uint32_t vga_mod = sys_clk % pixel_clk;
  uint32_t vga_frac8 = ((vga_mod * 256) + (pixel_clk >> 1)) / pixel_clk; 


  for (uint i = 0; i < 4; i++) {
    pio_gpio_init(pio, out_base_pin + i);
    gpio_set_slew_rate(out_base_pin + i, GPIO_SLEW_RATE_FAST);
  }

  pio_sm_config cfg = vga_pwm_pio_program_get_default_config(offset);

  sm_config_set_set_pins(&cfg, out_base_pin, 4);
  sm_config_set_out_pins(&cfg, out_base_pin, 4);
  
  sm_config_set_in_pin_base(&cfg, sync_in_pin);
  sm_config_set_in_pin_count(&cfg, 2);

  sm_config_set_clkdiv_int_frac8(&cfg, vga_div16, vga_frac8);

  pio_sm_init(pio, sm, offset, &cfg);

  pio_sm_set_consecutive_pindirs(pio, sm, out_base_pin, 4, true);
}
  
%}

